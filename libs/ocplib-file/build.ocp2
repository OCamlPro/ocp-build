(**************************************************************************)
(*                                                                        *)
(*   Typerex Libraries                                                    *)
(*                                                                        *)
(*   Copyright 2011-2017 OCamlPro SAS                                     *)
(*                                                                        *)
(*   All rights reserved.  This file is distributed under the terms of    *)
(*   the GNU Lesser General Public License version 2.1, with the          *)
(*   special exception on linking described in the file LICENSE.          *)
(*                                                                        *)
(**************************************************************************)

(* assert 'ocaml' exists *)
ocaml.exists = true;

(* assert fields 'asmcomp' and 'bytecomp' exists and correctly typed *)
ocaml = { asmcomp = []; bytecomp = [] } + ocaml;
begin
  ocaml.version = autoconf.package_version;
  ocaml.files = [
    "fileSig.ml";

    "fileOS.ml";
    "fileChannel.ml";

    "fileString.ml";

    "fileLabels.mli";
    "fileLabels.ml", { asmcomp = ocaml.asmcomp + [ "-nolabels" ];
                       bytecomp = ocaml.bytecomp + [ "-nolabels" ];
                     };
    "fileLines.ml";

    "fileGen.ml";

    "fileDir.ml";

    "filePath.ml";

    "file.ml"; (* For backward compatibility. To be removed soon. *)
   ];

  ocaml.requires = [
    "ocplib-lang";
    "ocplib-unix";
  ];
   new_package("ocplib-file", "library", ocaml);
end
